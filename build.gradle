plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'  // ← Plugin de coverage
    id 'org.sonarqube' version '6.0.1.5171'  // ← Plugin do SonarQube
}

group = 'soat-project'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

bootJar {
	archiveFileName = 'app.jar'
}

jar {
	enabled = false
}

repositories {
	mavenCentral()
}

sourceSets {
	testIntegration {
		java {
			srcDir file('src/testIntegration/java')
		}
		resources {
			srcDir file('src/testIntegration/resources')
		}
		compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
		runtimeClasspath += output + compileClasspath
	}
}

tasks.withType(Copy).configureEach {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

apply from: 'gradle/test-integration.gradle'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	implementation ('org.springframework.boot:spring-boot-starter-web') {
		exclude module: 'spring-boot-starter-tomcat'
	}
	implementation 'org.springframework.boot:spring-boot-starter-undertow'

    implementation "software.amazon.awssdk:sts"

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	runtimeOnly 'org.postgresql:postgresql'

	// Spring WebFlux
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// Spring actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// ZXing
	implementation 'com.google.zxing:core:3.5.1'
	implementation 'com.google.zxing:javase:3.5.1'

	// Json Web Token (JWT)
	implementation('io.jsonwebtoken:jjwt-api:0.12.5')
	runtimeOnly('io.jsonwebtoken:jjwt-impl:0.12.5')
	runtimeOnly('io.jsonwebtoken:jjwt-jackson:0.12.5')

    //aws
    implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs"

	// Spring Jakarta Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Testcontainers para PostgreSQL
	testImplementation("org.testcontainers:junit-jupiter:1.19.7")
	testImplementation("org.testcontainers:postgresql:1.19.7")

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation "org.mockito:mockito-core:5.11.0"
    testImplementation "org.mockito:mockito-inline:5.2.0"





}
ext {
    projectVersion = "3.0.2"
}


dependencyManagement {
    imports {
        mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${projectVersion}"
    }
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/Application.class',
                    '**/adapter/**',
                    '**/port/**',
                    '**/config/**',
                    '**/dto/**',
                    '**/entity/**'
            ])
        }))
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    jvmArgs(
            "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('byte-buddy-agent') }}",
            "-XX:+EnableDynamicAgentLoading"
    )
}

// Configuração do SonarQube
sonar {
    properties {
        property "sonar.projectKey", "SOAT-Project_fastfood-order-service"
        property "sonar.organization", "soat-project"
        property "sonar.host.url", "https://sonarcloud.io"

        property "sonar.java.source", "21"
        property "sonar.sourceEncoding", "UTF-8"

        // Diretórios
        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java"
        property "sonar.java.binaries", "build/classes/java/main"
        property "sonar.java.test.binaries", "build/classes/java/test"

        // Coverage
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"

        // Exclusões
        property "sonar.exclusions", [
                "**/Dockerfile",
                "**/*.md",
                "**/*.yml",
                "**/*.yaml",
                "**/*.json",
                "**/*.txt",
                "**/resources/**/*.xml",
                "**/resources/**/*.properties"
        ].join(",")

        property "sonar.coverage.exclusions", [
                "**/Application.java",
                "**/adapter/**",
                "**/port/**",
                "**/config/**",
                "**/dto/**",
                "**/entity/**"
        ].join(",")

        property "sonar.test.exclusions", "src/test/**/*"
        property "sonar.cpd.exclusions", "src/test/**/*"
    }
}



tasks.named('test') {
	useJUnitPlatform()
}